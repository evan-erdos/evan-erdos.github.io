---
layout: sketch
title: Synaptic Chaos
tag: [Programming, GameDev]
permalink: /sketch/mania.coffee/
script: /js/mania.js
---

**This is not complete, nor is it clean, and you should also try sometimes not doing cocaine.**

```coffee
### Ben Scott # 2015-10-12 # Mania ###

'use strict' # just like JavaScript
```


### `P5.js` Main class ###

This is our instance of the main class in the `P5.js` library. The argument is the link between the library and this code, and the special functions we override in the class definition are callbacks for P5.js events.

```coffee
myp = new p5 (p) ->

    ### Input ###
    mouse = [p.mouseX,p.mouseY]
    lastMouse = [p.pmouseX,p.pmouseY]

    ### DOM ###
    [container,canvas] = [null,null]

    ### Resources ###
    [bg_img,dope_img] = [null,null]

    ### Domain ###
    dope_rate = 60
    manic = false
    neurotransmitters = []
    receptors = []
```

### `Dopamine` ###

This is a class which represents the neurotransmitter Dopamine, which is responsible for pleasure and euphoria.

- `@a`: acceleration
- `@v`: velocity
- `@t`: time of lifespan
- `@m`: mass
- `@max_v`: maximum speed
- `@max_f`: maximum force
- `@r`: radius
- `@pos`: current position
- `@tgt`: target position

```coffee
    class Dopamine
        max_v = 3
        max_f = 0.05
        @tgt = null
        @t = 360

        constructor: (@r=10,@pos,@m=1,@t=1024) ->
            @a = p.createVector(0,0)
            @v = p.createVector(
                p.random(-1,1)
                p.random(-1,1))
            @tgt = p.createVector(p.width/2,p.height/2)
            if (@pos==null || @pos==undefined)
                @pos = p.createVector(
                    p.width/2+p.random(-50,50),-10)
                @v.y = p.random(1,2)

        run: (group) ->
            @flock(group)
            @update()
            @bounds()
            @draw()

        applyForce: (f) -> @a.add(f)/@m

        flock: (group) ->
            sep = @separate(group)
            ali = @align(group)
            coh = @cohesion(group)
            final_steer = @seek(
                p.createVector(p.width/2,p.height*2))

            sep.mult(1.5)
            ali.mult(1)
            coh.mult(1)
            final_steer.mult(0.5)

            @applyForce(sep)
            @applyForce(ali)
            @applyForce(coh)
            @applyForce(final_steer)

        bounds: ->
            return if (-20<@pos.x<p.width+20)
            unless (-20<@pos.y<p.height+20)
                neurotransmitters.remove(this)
                delete this

        update: ->
            @v.add(@a)
            @v.limit(max_v)
            @pos.add(@v)
            @a = p.createVector(0,0)

        seek: (tgt) ->
            @tgt = tgt
            len = p5.Vector.sub(tgt,@pos)
            len.normalize()
            len.mult(max_v)
            dir = p5.Vector.sub(len,@v)
            dir.limit(0.05)
            return dir

        draw: ->
            p.push()
            value = p.map(value,0,255,60,120)
            p.fill(value,180,180)
            p.stroke(180)
            p.strokeWeight(2)
            p.translate(p.random(3),p.random(3)) if (manic)
            p.line(@pos.x,@pos.y,@pos.x-8,@pos.y-4)
            p.line(@pos.x,@pos.y,@pos.x-8,@pos.y+4)
            p.line(@pos.x,@pos.y,@pos.x+8,@pos.y-4)
            p.line(@pos.x+8,@pos.y-4,@pos.x+12,@pos.y-2)
            polygon(@pos.x,@pos.y,5,6)
            p.pop()

        separate: (group) ->
            [tgt,n] = [20,0]
            dir = p.createVector(0,0)
            for elem in group
                d = p5.Vector.dist(@pos,elem.pos)
                if (0<d<tgt)
                    diff = p5.Vector.sub(@pos,elem.pos)
                    diff.normalize()
                    diff.div(d)
                    dir.add(diff)
                    n++
            dir.div(n) if (n>0)
            if (0<dir.mag())
                dir.normalize()
                dir.mult(max_v)
                dir.sub(@v)
                dir.limit(0.05)
            return dir

        align: (group) ->
            [len,n] = [50.0,0]
            sum = p.createVector(0,0)
            for elem in group
                d = p5.Vector.dist(@pos,elem.pos)
                if (0<d<len)
                    sum.add(elem.v)
                    n++
            if (n>0)
                sum.div(n)
                sum.normalize()
                sum.mult(max_v)
                dir = p5.Vector.sub(sum,@v)
                dir.limit(0.05)
                return dir
            else return p.createVector(0,0)

        cohesion: (group) ->
            [len,n] = [50,0]
            dir = p.createVector(0,0)
            for elem in group
                d = p5.Vector.dist(@pos,elem.pos)
                if (0<d<=len)
                    dir.add(elem.pos)
                    n++
            if (n>0)
                dir.div(n)
                return @seek(dir)
            else return p.createVector(0,0)
```

### `Amphetamine` ###

The same neuron! Now with amphetamines! Amphetamines act as a releasing agent for dopamine, and are much easier to animate!

- `@n_tgt`: final target (clearing behaviour)

```coffee
    class Amphetamine extends Dopamine
        @n_tgt = null

        constructor: ->
            super
            @n_tgt = p.createVector(
                p.width/2-128+p.random(-2,2)
                p.height/2-100+p.random(-2,2))
            if (p.random(3)>1)
                @n_tgt = p.createVector(
                    p.width/2+128+p.random(-2,2)
                    p.height/2-100+p.random(-2,2))

        update: ->
            super
            @t--
            #console.log("#{@t}")
            if (@t<0)
                if (p.random(2)>1)
                    @applyForce(@seek(p.createVector(
                        p.width*2
                        p.random(-30,30))).mult(3))
                else @applyForce(@seek(p.createVector(
                        p.width/2,-p.height).mult(3)))

        draw: ->
            p.push()
            p.fill(255,180,180)
            p.stroke(180)
            p.strokeWeight(2)
            p.line(@pos.x,@pos.y,@pos.x+8,@pos.y-4)
            p.line(
                @pos.x+8,@pos.y-4
                @pos.x+12,@pos.y-2)
            polygon(@pos.x,@pos.y,5,6)
            p.pop()

        flock: (group) ->
            sep = @separate(group)
            ali = @align(group)
            coh = @cohesion(group)

            sep.mult(1)
            ali.mult(0)
            coh.mult(0)

            @applyForce(sep)
            @applyForce(ali)
            @applyForce(coh)
            @applyForce(@seek(@n_tgt))

        separate: (group) ->
            [tgt,n] = [20,0]
            dir = p.createVector(0,0)
            for elem in group
                d = p5.Vector.dist(@pos,elem.pos)
                if (0<d<tgt)
                    diff = p5.Vector.sub(elem.pos,@pos)
                    diff.normalize()
                    diff.div(d)
                    dir.add(diff)
                    #console.log(elem instanceof Cocaine)
                    unless (elem instanceof Cocaine)
                        elem.applyForce(diff.normalize())
                    n++
            dir.div(n) if (n>0)
            if (0<dir.mag())
                dir.normalize()
                dir.mult(3)
                dir.sub(@v)
                dir.limit(0.1)
            return dir
```


### `Events` ###

These functions are automatic callbacks for `P5.js` events:

- `p.preload` is called once, immediately before `setup`
- `p.setup` is called once, at the beginning of execution
- `p.draw` is called as frequently as `p.framerate`
- `p.keyPressed` is called on every key input event
- `p.mousePressed` is called on mouse down
- `p.windowResized` keeps window full
- `p.remove` destroys everything in the sketch

```coffee
    p.preload = ->
        bg_img = p.loadImage("/rsc/sketch/synapse.png")

    p.setup = ->
        setupCanvas()
        p.noStroke()
        #setupDOM()
        p.frameRate(60)
        receptors.push(new Receptor(
            p.createVector(p.width/2,p.height/2)))

    p.draw = ->
        p.clear()
        p.background(bg_img)
        getInput()
        manic = (dope_rate<60)
        drawReceptors()
        if (dope_rate<60 && p.frameCount%90==0)
            dope_rate++
        return unless (p.frameCount%dope_rate==0)
        if (neurotransmitters.length<100)
            n = if (dope_rate<55) then 30 else 5
            for i in [0..p.random(2,n)]
                neurotransmitters.push(new Dopamine())
        for dope in neurotransmitters
            unless dope==null || dope==undefined
                dope.run(neurotransmitters)
        #drawDOM()

    p.keyPressed = ->
        alt = !alt if (p.keyCode is p.ALT)

    p.mouseDragged = ->
        mouse = [p.mouseX,p.mouseY]
        lastMouse = [p.pmouseX,p.pmouseY]

    p.mousePressed = ->
        pos = p.createVector(p.mouseX,p.mouseY)
        neurotransmitters.unshift(
            new Amphetamine(10,pos,p.createVector(0,0)))
        dope_rate--
```



